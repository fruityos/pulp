; ram disk

sub ramfs_getDataBlockSize()
	return 1 << 10
end

sub ramfs_init(ramfs)
	local dot
	ramfs_setRoot(ramfs, inode_create("/", INODE_TYPE_DIR()))
	dot = ramfs_createFile(ramfs, "/", ".", INODE_TYPE_DIR())
	inode_setContent(dot, ramfs_getRoot(ramfs))
end

sub ramfs_cleanup(ramfs)
	inode_destroy(ramfs_getRoot())
end

sub ramfs_getSize()
	return (1 << 3)
end

sub ramfs_getRoot(ramfs)
	return [ramfs + (0 << 3)]
end

sub ramfs_setRoot(ramfs, rootNode)
	[ramfs + (0 << 3)] = rootNode
end

sub ramfs_printTree(ramfs)
	local inode
	inode = ramfs_getRoot(ramfs)
	ramfs_printTree_r(inode, 0)	
end

sub ramfs_printTree_r(inode, level)
	local i
	while i < level do
		kputs("    ")
		i = i + 1
	end

	if inode_getType(inode) == INODE_TYPE_FILE() then
		kprintln(inode_getName(inode))
	else
		if inode_getType(inode) == INODE_TYPE_DIR() then
			kputs("[")
			kputs(inode_getName(inode))
			kputs("]")
			kputch(10)
			if kstrcmp(inode_getName(inode), ".") | kstrcmp(inode_getName(inode), "..") then
				return 0
			end
			inode = inode_getContent(inode)
			while inode != 0 do
				ramfs_printTree_r(inode, level + 1)
				inode = fba_getNext(inode)
			end
		else
			kprintln("Bad inode type")
		end
	end
end

sub ramfs_lookupPath(ramfs, path)
	local tmpPath, i, len, inode, dir, dirLen

	tmpPath = heap_alloc(1024)
	kstrcpy(tmpPath, path)
	len = kstrlen(tmpPath)
	while i < len do
		if byte [tmpPath + i] == byte ["/"] then
			byte [tmpPath + i] = 0
		end
		i = i + 1
	end
	i = 0
	inode = ramfs_getRoot(ramfs)
	while (i < len) & (inode != 0) do
		dir = tmpPath + i
		dirLen = kstrlen(dir)
		if dirLen > 0 then
			inode = inode_lookupByName(inode, dir)
		end
		i = i + dirLen + 1
	end
	heap_free(tmpPath, 1024)
	return inode
end

sub ramfs_createFile(ramfs, path, filename, type)
	local inode, parent

	parent = ramfs_lookupPath(ramfs, path)
	if (parent != 0) then
		if inode_lookupByName(parent, filename) == 0 then
			inode = inode_create(filename, type)
			inode_link(parent, inode)
			return inode
		end
	end
	return 0
end

sub ramfs_deleteFile(ramfs, path, filename)
	local inode, parent

	parent = ramfs_lookupPath(ramfs, path)
	if (parent != 0) then
		inode = inode_lookupByName(parent, filename)
		if (inode != 0) then
			ramfs_deleteFile_r(ramfs, parent, inode)
		end
	end
end

sub ramfs_deleteFile_r(ramfs, parent, inode)
	local inode, content, next

	; special case for cycles (HACK)
	if kstrcmp(inode_getName(inode), ".") | kstrcmp(inode_getName(inode), "..") then
		inode_unlink(parent, inode)
		inode_destroy(inode)
		return 0
	end

	if inode_getType(inode) == INODE_TYPE_DIR() then
		content = inode_getContent(inode)
		while content != 0 do
			next = fba_getNext(content)
			ramfs_deleteFile_r(ramfs, inode, content)
			content = next
		end
	end
	if inode_getType(inode) == INODE_TYPE_FILE() then
		content = inode_getContent(inode)
		while content != 0 do
			next = fba_getNext(content)
			heap_free(content, ramfs_getDataBlockSize())
			content = next
		end 
	end 
	inode_unlink(parent, inode)
	inode_destroy(inode)
end
